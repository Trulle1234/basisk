---------------------------------------------------------------

In english / På engelska:
statements  : NEWLINE* expr (NEWLINE+ expr)* NEWLINE*

expr        : KEYWORD:let IDENTIFIER EQ expr
            | comp-expr ((KEYWORD:and | KEYWORD:or) comp-expr)*

comp-expr   : KEYWORD:not comp-expr
            | arith-expr ((EE | NE | LT | GT | LTE | GTE) arith-expr)*

arith-expr  : term ((PLUS | MINUS) term)*

term        : factor ((MUL | DIV) factor)*

factor      : (PLUS | MINUS) factor
            | power

power       : call (POW factor)*

call        : atom (LPAREN (expr (SEMI expr)*)? RPAREN)?

atom        : INT | FLOAT | STRING | IDENTIFIER
            | LPAREN expr RPAREN
            | list-expr
            | if-expr
            | for-expr
            | while-expr
            | func-def

list-expr   : LSQUARE (expr (SEMI expr)*)? RSQUARE

if-expr     : KEYWORD:if expr KEYWORD:then expr
            | KEYWORD:if expr KEYWORD:then NEWLINE statements KEYWORD:end
            | KEYWORD:if expr KEYWORD:then expr
              (KEYWORD:elif expr KEYWORD:then expr)*
              (KEYWORD:else expr)?
            | KEYWORD:if expr KEYWORD:then NEWLINE statements
              (KEYWORD:elif expr NEWLINE statements)*
              (KEYWORD:else NEWLINE statements)? KEYWORD:end

for-expr    : KEYWORD:for IDENTIFIER EQ expr KEYWORD:to expr
              (KEYWORD:step expr)? KEYWORD:then expr
            | KEYWORD:for IDENTIFIER EQ expr KEYWORD:to expr
              (KEYWORD:step expr)? KEYWORD:then NEWLINE statements KEYWORD:end

while-expr  : KEYWORD:while expr KEYWORD:then expr
            | KEYWORD:while expr KEYWORD:then NEWLINE statements KEYWORD:end

func-def    : KEYWORD:def IDENTIFIER? LPAREN (IDENTIFIER (SEMI IDENTIFIER)*)? RPAREN
              (ARROW statements | NEWLINE statements KEYWORD:end)

---------------------------------------------------------------

In swedish / På svenska:

satser               : NYRAD* uttryck (NYRAD+ uttryck)* NYRAD*

uttryck              : NYCKELORD:låt IDENTIFIERARE = uttryck
                     | jämförelse ((NYCKELORD:och | NYCKELORD:eller) jämförelse)*

jämförelse           : NYCKELORD:inte jämförelse
                     | aritmetik ((== | != | < | > | <= | >=) aritmetik)*

aritmetik            : term ((+ | -) term)*

term                 : faktor ((* | /) faktor)*

faktor               : (+ | -) faktor
                     | potens

potens               : anrop (^ faktor)*

anrop                : atom ( ( (uttryck ( ; uttryck )*)? ) )?

atom                 : HELTAL | FLYTTAL | STRÄNG | IDENTIFIERARE
                     | ( uttryck )
                     | lista
                     | om-uttryck
                     | för-uttryck
                     | medan-uttryck
                     | funktions-definition

lista                : [ ( uttryck ( ; uttryck )* )? ]

om-uttryck           : om uttryck då uttryck
                     | om uttryck då NYRAD satser slut
                     | om uttryck då uttryck
                       (annars_om uttryck då uttryck)*
                       (annars uttryck)?
                     | om uttryck då NYRAD satser
                       (annars_om uttryck NYRAD satser)*
                       (annars NYRAD satser)? slut

för-uttryck          : för IDENTIFIERARE = uttryck till uttryck
                       (steg uttryck)? då uttryck
                     | för IDENTIFIERARE = uttryck till uttryck
                       (steg uttryck)? då NYRAD satser slut

medan-uttryck        : medan uttryck då uttryck
                     | medan uttryck då NYRAD satser slut

funktions-definition : definiera IDENTIFIERARE?
                       (IDENTIFIERARE ( ; IDENTIFIERARE )*)?
                       -> satser
                     | definiera IDENTIFIERARE?
                       ( (IDENTIFIERARE ( ; IDENTIFIERARE )*)? )
                       NYRAD satser slut

---------------------------------------------------------------