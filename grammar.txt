---------------------------------------------------------------

In english / På engelska:

expr              : KEYWORD:låt IDENTIFIER EQ expr
                    | comp-expr ((KEYWORD:och | KEYWORD:eller) comp-expr)*

comp-expr         : KEYWORD:inte comp-expr
                    | arith-expr ((EE | NE | LT | GT | LTE | GTE) arith-expr)*

arith-expr        : term ((PLUS | MINUS) term)*

term              : factor ((MUL | DIV) factor)*

factor            : (PLUS | MINUS) factor
                    | power

power             : call (POW factor)*

call              : atom (LPAREN (expr (SEMI expr)*)? RPAREN)?

atom              : INT | FLOAT | IDENTIFIER
                    | LPAREN expr RPAREN
                    | if-expr
                    | for-expr
                    | while-expr
                    | func-def

if-expr           : KEYWORD:om expr KEYWORD:då expr
                      (KEYWORD:annars_om expr KEYWORD:då expr)*
                      (KEYWORD:annars expr)?

for-expr          : KEYWORD:för IDENTIFIER EQ expr KEYWORD:till expr
                      (KEYWORD:steg expr)? KEYWORD:då expr

while-expr        : KEYWORD:medan expr KEYWORD:då expr

func-def          : KEYWORD:definera IDENTIFIER?
                      LPAREN (IDENTIFIER (SEMI IDENTIFIER)*)? RPAREN
                      ARROW expr

---------------------------------------------------------------

In swedish / På svenska:

uttryck              : NYCKELORD:låt IDENTIFIERARE = uttryck
                        | jämförelse-uttryck ((NYCKELORD:och | NYCKELORD:eller) jämförelse-uttryck)*

jämförelse-uttryck   : NYCKELORD:inte jämförelse-uttryck
                        | aritmetiskt-uttryck ((== | != | < | > | <= | >=) aritmetiskt-uttryck)*

aritmetiskt-uttryck  : term ((+ | -) term)*

term                 : faktor ((* | /) faktor)*

faktor               : (+ | -) faktor
                        | potens

potens               : anrop (^ faktor)*

anrop                : atom ( ( uttryck ( ; uttryck )* )? )?

atom                 : HELTAL | FLYTTAL | IDENTIFIERARE
                        | ( uttryck )
                        | om-uttryck
                        | för-uttryck
                        | medan-uttryck
                        | funktions-definition

om-uttryck           : NYCKELORD:om uttryck NYCKELORD:då uttryck
                          (NYCKELORD:annars_om uttryck NYCKELORD:då uttryck)*
                          (NYCKELORD:annars uttryck)?

för-uttryck          : NYCKELORD:för IDENTIFIERARE = uttryck NYCKELORD:till uttryck
                          (NYCKELORD:steg uttryck)? NYCKELORD:då uttryck

medan-uttryck        : NYCKELORD:medan uttryck NYCKELORD:då uttryck

funktions-definition : NYCKELORD:definera IDENTIFIERARE?
                          ( (IDENTIFIERARE ( ; IDENTIFIERARE )*)? )
                          -> uttryck

---------------------------------------------------------------